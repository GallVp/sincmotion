plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.0'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'maven-publish'
    id "org.jetbrains.dokka" version "1.4.30"
    id "org.jetbrains.kotlin.native.cocoapods" version "1.5.0"
}

group = 'com.github.gallvp'
version = '0.1.1'

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

import org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask

kotlin {
    android {
        publishAllLibraryVariants()
    }
    ios {
    }

    tasks.register("debugFatFramework", FatFrameworkTask) {
        baseName = "SincMotion"
        destinationDir = file("$buildDir/fat-framework/debug")
        from(
                targets.iosX64.binaries.getFramework("DEBUG"),
                targets.iosArm64.binaries.getFramework("DEBUG")
        )
    }

    tasks.register("releaseFatFramework", FatFrameworkTask) {
        baseName = "SincMotion"
        destinationDir = file("$buildDir/fat-framework/release")
        from(
                targets.iosX64.binaries.getFramework("RELEASE"),
                targets.iosArm64.binaries.getFramework("RELEASE")
        )
    }

    cocoapods {
        summary = "SincMotion Library"
        homepage = "https://github.com/GallVp/SincMotionMP"
        authors = "Usman Rashid"
        ios.deploymentTarget = "13.0"

        frameworkName = "SincMotion"
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "com.github.gallvp:sincmathsmp:0.1"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'androidx.test.espresso:espresso-core:3.3.0'
            }
        }
        iosMain {

        }
        iosTest {

        }
    }
}

android {
    defaultConfig {
        ndkVersion "21.3.6528147"
        buildToolsVersion "30.0.2"
        compileSdkVersion 30
        targetSdkVersion 30
        minSdkVersion 16

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
}